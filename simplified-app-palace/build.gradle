import org.librarysimplified.gradle.RequiredAssetsTask

apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'

// Fail the build if these assets aren't present
//
def requiredFiles = [:]
requiredFiles["ReaderClientCert.sig"] =
  "b064e68b96e258e42fe1ca66ae3fc4863dd802c46585462220907ed291e1217d"
requiredFiles["secrets.conf"] =
  "35dbb18c0e97a6f0e3d9745c2f2ebff1a915202daa8905f650a742071a751f27"

android {
  defaultConfig {
    versionName = project.version
    versionCode = calculateVersionCode(project)
    setProperty("archivesBaseName", "palace")
  }
  applicationVariants.all { variant ->
    def finalizerTask =
      tasks.create(name: "requiredFiles${variant.name.capitalize()}", type: RequiredAssetsTask) {
        required = requiredFiles
        apkFiles = variant.outputs.collect { out ->
          out.outputFile
        }
      }
    variant.assembleProvider.configure {
      finalizedBy finalizerTask
    }
  }
}

configurations.all {
  exclude group: 'org.librarysimplified.drm'
}

dependencies {
  implementation project(":simplified-main")
  implementation project(":simplified-accounts-source-nyplregistry")
  implementation project(":simplified-analytics-circulation")
  implementation project(":simplified-crashlytics")

  implementation libs.firebase.analytics
  implementation libs.firebase.crashlytics
  implementation libs.nypl.readium
  implementation libs.palace.drm.adobe
  implementation libs.palace.findaway
  implementation libs.palace.overdrive
  implementation libs.r2.lcp
  implementation libs.r2.opds
  implementation libs.r2.shared
  implementation libs.r2.streamer

  implementation (libs.readium.lcp) {
    artifact {
      type = "aar"
    }
  }
}
