import org.librarysimplified.gradle.RequiredAssetsTask

apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'

// Fail the build if these assets aren't present
//
def requiredFiles = [:]
requiredFiles["ReaderClientCert.sig"] =
  "ac000b9942b782752bbf32918d34363d5da4e8df0b3fb617c923bcbad3996c6a"
requiredFiles["secrets.conf"] =
  "9c211ae0d153c29b72baab3bfad9de6c5cecb6b39bcda8206d36a65aaca0db21"

android {
  defaultConfig {
    versionName = project.version
    versionCode = calculateVersionCode(project)
    setProperty("archivesBaseName", "palace")
  }
  applicationVariants.all { variant ->
    def finalizerTask =
      tasks.create(name: "requiredFiles${variant.name.capitalize()}", type: RequiredAssetsTask) {
        required = requiredFiles
        apkFiles = variant.outputs.collect { out ->
          out.outputFile
        }
      }
    variant.assembleProvider.configure {
      finalizedBy finalizerTask
    }
  }
}

dependencies {
  implementation project(":simplified-main")
  implementation project(":simplified-accounts-source-nyplregistry")
  implementation project(":simplified-analytics-circulation")
  implementation project(":simplified-crashlytics")
//  implementation project(":simplified-migration-from3master")
  implementation libraries.firebase_analytics
  implementation libraries.firebase_crashlytics
  implementation libraries.readium_lcp
  implementation libraries.nypl_drm_core
  implementation libraries.nypl_drm_adobe
  implementation libraries.nypl_findaway
//  implementation libraries.nypl_overdrive

  annotationProcessor libraries.google_autovalue_processor
}
